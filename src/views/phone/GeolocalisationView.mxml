<?xml version="1.0" encoding="utf-8"?>
<common:RootNavigatorView xmlns:fx="http://ns.adobe.com/mxml/2009"
						  xmlns:common="views.common.*" creationComplete="init()"
						  xmlns:s="library://ns.adobe.com/flex/spark">
	<fx:Script>
		<![CDATA[
			/**
			 * Based on Tour De Flex 'SampleGeolocalisation'. 
			 */
			import flash.events.GeolocationEvent;
			import flash.sensors.Geolocation;
			
			import mx.events.FlexEvent;
			protected var g:Geolocation = new Geolocation();    
			
			protected function init():void  
			{
				if (Geolocation.isSupported)
				{
					if (g.muted)
					{
						log.text = "Access to GPS has been muted";
						return;
					}
					
					log.text = "Finding Location...";
					g.addEventListener(GeolocationEvent.UPDATE, onUpdate);
					addEventListener(FlexEvent.REMOVE, onRemove);
				}
				else
				{
					currentState = "unsupported";
					lblSupport.text = "Geolocation is not supported on this device.";
				}    
			}
			
			protected function onUpdate(event:GeolocationEvent):void
			{
				trace("Update event called");
				var mph:Number = event.speed*2.23693629;
				var kph:Number = event.speed*3.6;
				log.text = "Updated: " + new Date().toTimeString() + "\n\n"
					+ "latitude: " + event.latitude.toString() + "\n"
					+ "longitude: " + event.longitude.toString() + "\n"
					+ "altitude: " + event.altitude.toString() + "\n"
					+ "speed: " + event.speed.toString() + "\n"
					+ "speed: " + mph.toString() + " MPH \n"
					+ "speed: " + kph.toString() + " KPH \n"
					+ "heading: " + event.heading.toString() + "\n"
					+ "horizontal accuracy: "
					+ event.horizontalAccuracy.toString() + "\n"
					+ "vertical accuracy: "
					+ event.verticalAccuracy.toString();        
			}
			
			protected function onRemove(event:FlexEvent):void
			{
				g.removeEventListener(GeolocationEvent.UPDATE, onUpdate);                
			}
			
		]]>
	</fx:Script>
	<common:states>
		<s:State name="normal"/>
		<s:State name="unsupported"/>			
	</common:states>
	
	
	<common:layout>
		<s:VerticalLayout gap="40" paddingBottom="20" paddingLeft="20" paddingRight="20" paddingTop="20"/>
		
	</common:layout>
	
	<s:Label id="lblSupport" includeIn="unsupported" width="95%"/>
	<s:TextArea includeIn="normal" width="98%"
				text="The Geolocation sample displays updates to the current geographical location of the device in the form of latitudinal and longitudinal coordinates."/>
	<s:VGroup includeIn="normal" width="400">
		<s:Label text="Event Log:"/>
		<s:TextArea id="log" height="400" editable="false"/>
	</s:VGroup>
</common:RootNavigatorView>
